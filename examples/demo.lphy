N_max ~ LogNormal(meanlog=9.16, sdlog=2.0);
t50 ~ LogNormal(meanlog=3.9, sdlog=0.64);
b ~ LogNormal(meanlog=-2.2, sdlog=1.0);
NA ~ LogNormal(meanlog=5.0, sdlog=2.0);
I_na ~ UniformDiscrete(lower=0, upper=1);

exponentialPopFunc = exponentialPopFunc(GrowthRate=b, N0=N_max, NA=NA, I_na=I_na);
logisticPopFunc = logisticPopFunc(t50=t50, nCarryingCapacity=N_max, b=b, NA=NA, I_na=I_na);
gompertzPopFunc = gompertzPopFunc_t50(NInfinity=N_max,t50=t50, b=b, NA=NA, I_na=I_na);

models = [exponentialPopFunc,logisticPopFunc, gompertzPopFunc];
I ~ UniformDiscrete(lower=0, upper=length(models)-1);
SVSmodel = stochasticVariableSelection(indicator=I, models=models);
tree ~CoalescentPopFunc(n=16, popFunc=SVSmodel);
κ ~ LogNormal(meanlog=1.0, sdlog=0.5);
π ~ Dirichlet(conc=[2.0,2.0,2.0,2.0]);
D ~ PhyloCTMC(L=200, Q=hky(kappa=κ, freq=π), mu=0.01, tree=tree);
height = tree.rootAge();
length = tree.treeLength();
